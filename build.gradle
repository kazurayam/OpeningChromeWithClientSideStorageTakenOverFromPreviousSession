plugins {
    id 'groovy'
    id "com.katalon.gradle-plugin" version "0.0.7"
}

group = 'com.kazurayam'
jar.archiveBaseName = "webdriverfactory4ks"
version = '0.3.0'

ext {
    groovyVersion   = '2.4.7'
}

configurations {
    generateDocs
}

repositories {
    jcenter()
    mavenCentral()
}

katalon {
    dependencyPrefix = "com.kazurayam"
    minimize = false
}

shadowJar {
    exclude 'Temp*.class'
    classifier = null
}

// execute `gradle clean katalonPluginPackage`

dependencies {
    generateDocs "org.codehaus.groovy:groovy-all:${groovyVersion}"
}

sourceSets {
    main {
        groovy {
            srcDirs = ['Keywords', 'Include/scripts/groovy']
            srcDir 'Libs'
        }
    }
}

task groovydoc(type: Groovydoc, overwrite:true) {
    source = sourceSets.main.groovy
    classpath = configurations.compile
    groovyClasspath = project.configurations.generateDocs
    include 'com/kazurayam/thoughtfulwebdriverfactory/*'
    exclude '**/*Test.groovy'
}
task publishGroovydoc(type: Copy) {
    from 'build/docs/groovydoc'
    into 'docs/api'
}
groovydoc.finalizedBy publishGroovydoc
