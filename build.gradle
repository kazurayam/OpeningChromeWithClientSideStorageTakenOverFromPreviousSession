plugins {
    id 'groovy'
    id "com.katalon.gradle-plugin" version "0.0.7"
}

group = 'com.kazurayam'
jar.archiveBaseName = "webdriverfactory4ks"
version = '0.4.0-SNAPSHOT'

ext {
    AUTO_IMPORTED_JAR_PREFIX = 'AUTOIMPORTED_'
    webdriverfactoryVersion = '0.1.0-SNAPSHOT'
    groovyVersion = '2.4.20'
    junit4ksVersion = '1.6.1'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    myconf
    generateDocs
}

dependencies {
	myconf group: 'com.kazurayam', name: 'webdriverfactory',
	        version: "${webdriverfactoryVersion}"
	myconf group: 'com.kazurayam', name: 'junit4ks',
	        version: "${junit4ksVersion}"
	generateDocs "org.codehaus.groovy:groovy-all:${groovyVersion}"
}

task drivers {
	doFirst {
	    delete fileTree("Drivers") {
	        include("**/" + AUTO_IMPORTED_JAR_PREFIX + "*")
	    }
	}
	doLast {
	    copy { copySpec ->
	        copySpec
	            .from(project.getConfigurations().getByName("myconf"))
	            .into("Drivers")
	            .include(
	                "**/webdriverfactory*.jar",
	                "**/junit4ks*.jar"
	                )
	            .rename({ s ->
	                AUTO_IMPORTED_JAR_PREFIX + s
	                })
	    }
	}
}

katalon {
    dependencyPrefix = "com.kazurayam"
    minimize = false
}

shadowJar {
    exclude 'Temp*.class'
    classifier = null
}

// execute `gradle clean katalonPluginPackage`

sourceSets {
    main {
        groovy {
            srcDirs = ['Keywords', 'Include/scripts/groovy']
            srcDir 'Libs'
        }
    }
}

task groovydoc(type: Groovydoc, overwrite:true) {
    group = 'com.kazurayam'
    source = sourceSets.main.groovy
    classpath = configurations.compile
    groovyClasspath = project.configurations.generateDocs
    include 'com/kazurayam/ks/webdriverfactory4ks/*'
    exclude '**/*Test.groovy'
}
task publishGroovydoc(type: Copy) {
    from 'build/docs/groovydoc'
    into 'docs/api'
}
groovydoc.finalizedBy publishGroovydoc
